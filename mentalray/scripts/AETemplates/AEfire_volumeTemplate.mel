// Browse file code from https://github.com/ADN-DevTech/Maya-Locator

global proc AEfire_volumeNameNew (string $fileNameAttr) {
	setUITemplate -pst attributeEditorTemplate;
		columnLayout -adj true;
			rowLayout -nc 3 fileNameLayout;

        		text -label "Data file" fileNameText;
		        textField fileNameField;
					symbolButton -image "navButtonBrowse.png" browser;

            setParent ..;
		setParent ..;
	setParent ..;

	AEfire_volumeNameReplace ($fileNameAttr);
}

global proc AEfire_volumeNameReplace (string $fileNameAttr) {
	string $fileName = `getAttr $fileNameAttr`;
    string $nodeName = `plugNode $fileNameAttr`;

	// file name attribute
    connectControl -fileName fileNameField $fileNameAttr;
	button -e -c ("AEfire_volumeNameFileBrowser " + $fileNameAttr) browser;
}

global proc AEfire_volumeNameFileBrowser (string $fileNameAttr) {
    // choose a file to import
    string $result[] = `fileDialog2 -fileMode 1`;
    
    if (size($result) == 0 || size($result[0]) == 0) {
        // cancelled
        return;
    }

    // set the file name attribute of the cache node
    setAttr $fileNameAttr -type "string" $result[0];
}
global proc AEfire_volumeTemplate ( string $nodeName )
{
	//editorTemplate -beginNoOptimize;
	
	editorTemplate -beginLayout "Density File Attributes" -collapse 0;

		editorTemplate -callCustom "AEfire_volumeNameNew" "AEfire_volumeNameReplace" "density_file";

		editorTemplate -addSeparator;

		editorTemplate  -annotation "File input format" -addControl "density_read_mode";

		editorTemplate -annotation "Scales inputs data with this value" -addControl "density_scale";
		editorTemplate -annotation "Offsets inputs data with this value" -addControl "density_offset";
			
	editorTemplate -endLayout;
	
	editorTemplate -beginLayout "Temperature File Attributes" -collapse 0;

		editorTemplate -callCustom "AEfire_volumeNameNew" "AEfire_volumeNameReplace" "temperature_file";

		editorTemplate -addSeparator;
		
		editorTemplate -annotation "File input format" -addControl "temperature_read_mode";

		editorTemplate -annotation "Scales inputs data with this value" -addControl "temperature_scale";
		editorTemplate -annotation "Offsets inputs data with this value" -addControl "temperature_offset";
		
	editorTemplate -endLayout;
	
	editorTemplate -beginLayout "Main Attributes" -collapse 0;

		editorTemplate -annotation "Interpolation method to be used when querying for points between voxels" -addControl "interpolation_mode";
		editorTemplate -annotation "Variety of fuel used for the fire" -addControl "fuel_type";
		editorTemplate -annotation "Controls how much a human eye has adapted to the rendered fire" -addControl "visual_adaptation_factor";
		editorTemplate -annotation "How intense is the emitted fire light" -addControl "intensity";
		editorTemplate -annotation "If the contribution is smaller than the threshold, no shadow rays for the sample will be computed" -addControl "shadow_threshold";
		editorTemplate -annotation "Light extinction over distance" -addControl "decay";
		editorTemplate -annotation "Quality parameter, ideally it should be set to half the voxel size" -addControl "march_increment";
		editorTemplate -annotation "If true, computes how light varies when traversing the medium" -addControl "cast_shadows";
	
		editorTemplate -addSeparator;
		
		editorTemplate -annotation "Input auxiliary shader, safe to ignore" -addControl "density_shader";
		editorTemplate -annotation "Input auxiliary shader, safe to ignore" -addControl "absorption_shader";
		
	editorTemplate -endLayout;
	
	editorTemplate -suppress "color";
	editorTemplate -suppress "glowColor";
	editorTemplate -suppress "matteOpacity";
	editorTemplate -suppress "transparency";

	editorTemplate -suppress "caching"; 
	editorTemplate -suppress "nodeState"; 
	
	//editorTemplate -endNoOptimize;
}
