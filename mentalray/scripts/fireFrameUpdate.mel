proc string fireNewFileName(string $filename, int $currentFrame){
	// Separate filename in name and extension
	$tokenFilename = `stringToStringArray $filename "."`;
	
	// Get the file number
	string $numberExpr = "[0-9]+$"; // A bunch of digits at the end of the string
	string $fileNumberStr = `match $numberExpr $tokenFilename[0]`;
	$fileNumberSize = `size $fileNumberStr`;
	
	// Get the name without the numbers
	$nameSize = `size $tokenFilename[0]`;
	$nameSize = $nameSize - $fileNumberSize;
	$fileName = `substring $tokenFilename[0] 1 $nameSize`;
	
	// Increment to current frame
	$fileNumber = (int)$fileNumberStr;
	$fileNumber = $fileNumber + $currentFrame - 1;
	$fileNumberStr = (string)$fileNumber;
	$newNumSize = `size $fileNumberStr`;
	
	if($newNumSize < $fileNumberSize){
		// Add extra zeros to get the same size
		int $size = $fileNumberSize - $newNumSize;
		int $i;
		for ($i = 0; $i < $size; $i++) {
			$fileNumberStr = `stringAddPrefix $fileNumberStr "0"`;
		}
	}
	
	// Put together the new file name
	$newFile = $fileName + $fileNumberStr + "." + $tokenFilename[1];
	
	return $newFile;
}

global proc fireFrameUpdate(string $fireShader) {

	if(!`objExists $fireShader`){
		// Object was deleted, do nothing
		return;
	}
	
	$currentFrame = `currentTime -query`;

	$densityFileName = `getAttr ($fireShader + ".density_file_first")`;
	$densityFileName = `workspace -expandName $densityFileName`;
	$newFile = `fireNewFileName $densityFileName $currentFrame`;

	if(`filetest -r $newFile`){ // If file does not exits, do not update
		setAttr -type "string" ($fireShader + ".density_file") $newFile;
	}
	
	$temperatureFileName = `getAttr ($fireShader + ".temperature_file_first")`;
	$temperatureFileName = `workspace -expandName $temperatureFileName`;
	$newFile = `fireNewFileName $temperatureFileName $currentFrame`;
	
	if(`filetest -r $newFile`){ 
		setAttr -type "string" ($fireShader + ".temperature_file") $newFile;
	}
}
