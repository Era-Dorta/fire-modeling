global proc string createFireVolume( string $temperature_file, string $density_file){
	select -cl; // Clear selection

	// Create all the shading nodes
	$fire_shader = `shadingNode -asShader "fire_volume"`;
	$fireSG = `sets -renderable true -noSurfaceShader true -empty -name ($fire_shader + "SG")`;
	$temp_voxel = `shadingNode -asUtility "voxel_density"`;
	$density_voxel = `shadingNode -asUtility "voxel_density"`;
	$fire_light_shader = `shadingNode -asUtility "fire_volume_light"`;

	// Create the cube volume
	createImplicitVolume "cube";
	string $select_list[] = `ls -selection`;
	string $cube = $select_list[0];

	// Create an area light
	defaultAreaLight(1, 1, 1, 1, 0, 0, 0, 0, 0, 1, 0);
	string $select_list[] = `ls -selection`;
	string $area_light = $select_list[0];

	// Set the files paths in the voxel nodes
	setAttr -type "string" ($temp_voxel + ".filename") $temperature_file;
	setAttr -type "string" ($density_voxel + ".filename") $density_file;

	// Conect the voxel readers with the shading nodes
	connectAttr -f ($fire_shader + ".message") ($fireSG + ".volumeShader");
	connectAttr -f ($density_voxel + ".message") ($fire_shader + ".density_shader");
	connectAttr -f ($temp_voxel + ".message") ($fire_light_shader + ".temperature_shader");
	connectAttr -f ($fire_light_shader + ".message") ($area_light + ".miLightShader");
	
	// Set the read modes for the voxel data
	setAttr ($temp_voxel + ".read_mode") 2; // Max rgb value
	setAttr ($density_voxel + ".read_mode") 1; // Red channel only

	// Set the material of the cube to be the Shading Group 
	select -r $cube;
	sets -forceElement $fireSG;

	// Activate mental ray light type and custom light shader
	setAttr ($area_light + ".areaLight") 1;
	setAttr ($area_light + ".areaType") 4;

	// Make the cube and the light to be one object
	string $cube_transform = `firstParentOf $cube`;
	select -r $area_light $cube_transform;
	parent -s -r;

	// Lock all the transformations in the light
	setAttr -l true { ($area_light + ".t") };
	setAttr -l true { ($area_light + ".r") };
	setAttr -l true { ($area_light + ".s") };
	setAttr -l true { ($area_light + ".sh") };

	select -r $cube;
	return $cube;
}
